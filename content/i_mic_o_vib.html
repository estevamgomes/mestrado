<h1>Input: Som <img src="img/in_out-01.png"> Output: Vibração</h1>

<video autoplay controls>
  <source src="../img/videos_hi/i_mic_o_vib.mp4" type="video/mp4" />
</video>

<pre><code class="arduino">
/*
 * Input Electret Microphone [+calibration] > Output Analog
 * 
 * Description: Calibrate the readings of an Electret Microphone (sound level) that controls an analog output 
 * 
 * Recommended inputs: Sparkfun's breakout board for electret microphone
 * Recommended outputs: LED, Fan, DC Motor, Vibration Motor
 * 
 * Created 2017
 * By E. Quintino &lt;www.estevamgomes.com.br&gt;
 */

const int analogInPin  = A0;  // the number of the analog input pin
const int analogOutPin = 9;   // the number of the pwm output pin

int sensorValue = 0;          // store the value read from analog input
int outputValue = 0;          // store the value to output to the PWM (analog output)

// CALIBRATION
// based on http://www.arduino.cc/en/Tutorial/Calibration
int cal_sensorMin      = 1023; // minimum sensor value
int cal_sensorMax      = 0;    // maximum sensor value
const int cal_duration = 8000; // duration of calibration period
const int cal_weight   = 0.1;  // 

// MICROPHONE
// based on https://learn.sparkfun.com/tutorials/electret-mic-breakout-board-hookup-guide
const int record_duration = 100;  // sample window width in mS (250 mS = 4Hz)

void setup() {
  
  // CALIBRATION
  pinMode(13, OUTPUT);      // define pin 13 as output
  digitalWrite(13, HIGH);   // turn on LED to signal the start of the calibration

  int cal_start = millis();
  while (millis() - cal_start < cal_duration) {
    
    int record_start = millis();  // Start of sample window
    int record_max = 0;
    int record_min = 1024;
    
    // collect data for record_duration miliseconds
    while (millis() - record_start < record_duration) {
      // reads the sensor and stores into a variable
      int sensorReading = analogRead(0);
      
      if (sensorReading > record_max) {
        record_max = sensorReading;  // save just the max levels
      } else if (sensorReading < record_min) {
        record_min = sensorReading;  // save just the min levels
      }
    }
    
    int peakToPeak = record_max - record_min;  // peak-peak amplitude
    sensorValue = constrain(peakToPeak, 0, 1023);

    // record the maximum sensor value
    if (sensorValue > cal_sensorMax) {
      cal_sensorMax = sensorValue;
    }

    // record the minimum sensor value
    if (sensorValue < cal_sensorMin) {
      cal_sensorMin = sensorValue;
    }
  }
  
  // to get better results I suggest to reduce a bit the maximum and increase a bit the minimum
  int cal_range = cal_sensorMax - cal_sensorMin;
  cal_sensorMax = cal_sensorMax - cal_range * cal_weight;
  cal_sensorMin = cal_sensorMin + cal_range * cal_weight;
  
  digitalWrite(13, LOW); // turn off LED to signal the end of the calibration
}

void loop() {
  int record_start = millis();  // Start of sample window
  int record_max = 0;
  int record_min = 1024;
  
  // collect data for record_duration miliseconds
  while (millis() - record_start < record_duration) {
    // reads the sensor and stores into a variable
    int sensorReading = analogRead(0);
    
    if (sensorReading > record_max) {
      record_max = sensorReading;  // save just the max levels
    } else if (sensorReading < record_min) {
      record_min = sensorReading;  // save just the min levels
    }
  }

  int peakToPeak = record_max - record_min;  // peak-peak amplitude
  sensorValue = peakToPeak;

  // CALIBRATION
  // map the sensor reading from the calibration range to the output range (0-255)
  outputValue = map(sensorValue, cal_sensorMin, cal_sensorMax, 0, 255);
  outputValue = constrain(outputValue, 0, 255); // in case the sensor value is outside the range seen during calibration
  
  // UPDATE OUTPUT
  analogWrite(analogOutPin, outputValue); // change the analog output value

  delay(2); // wait 2 milliseconds before the next loop for the analog-to-digital converter to settle
}
</code></pre>