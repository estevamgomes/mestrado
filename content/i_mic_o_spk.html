<h1>Input: Som <img src="img/in_out-01.png"> Output: Som</h1>

<video autoplay controls>
  <source src="../img/videos_hi/i_mic_o_spk.mp4" type="video/mp4" />
</video>

<pre><code class="arduino">
/*
 * Input Electret Microphone [+calibration] > Output Sound
 * 
 * Description: Calibrate the readings of an Electret Microphone (sound level) that controls a speaker 
 * 
 * Recommended inputs: Sparkfun's breakout board for electret microphone
 * Recommended outputs: Sound
 * 
 * Created 2017
 * By E. Quintino &lt;www.estevamgomes.com.br&gt;
 */
 
const int analogInPin  = A0;  // the number of the analog input pin
const int analogOutPin = 9;   // the number of the pwm output pin

int sensorValue = 0;          // store the value read from analog input
int outputValue = 0;          // store the value to output to the PWM (analog output)

// CALIBRATION
// based on http://www.arduino.cc/en/Tutorial/Calibration
int cal_sensorMin      = 1023; // minimum sensor value
int cal_sensorMax      = 0;    // maximum sensor value
const int cal_duration = 8000; // duration of calibration period
float cal_weight       = 0.1;  // 

// MICROPHONE
// based on https://learn.sparkfun.com/tutorials/electret-mic-breakout-board-hookup-guide
const int record_duration = 100;  // sample window width in mS (250 mS = 4Hz)

// SPEAKER
unsigned long spk_lastBip = 0;
int spk_bipInterval       = 1000;

void setup() {
  
  // CALIBRATION
  pinMode(13, OUTPUT);      // define pin 13 as output
  digitalWrite(13, HIGH);   // turn on LED to signal the start of the calibration

  unsigned long cal_start = millis();
  while (millis() - cal_start < cal_duration) {
    
    int peakToPeakValue = peakToPeak(analogInPin, record_duration);
    sensorValue = constrain(peakToPeakValue, 0, 1023);

    // record the maximum sensor value
    if (sensorValue > cal_sensorMax) {
      cal_sensorMax = sensorValue;
    }

    // record the minimum sensor value
    if (sensorValue < cal_sensorMin) {
      cal_sensorMin = sensorValue;
    }
  }
  
  // to get better results I suggest to reduce a bit the maximum and increase a bit the minimum
  int cal_range = cal_sensorMax - cal_sensorMin;
  cal_sensorMax = cal_sensorMax - cal_range * cal_weight;
  cal_sensorMin = cal_sensorMin + cal_range * cal_weight;
  
  digitalWrite(13, LOW); // turn off LED to signal the end of the calibration
}

void loop() {
  sensorValue = peakToPeak(analogInPin, record_duration);

  // CALIBRATION
  sensorValue = constrain(sensorValue, cal_sensorMin, cal_sensorMax); // in case the sensor value is outside the range seen during calibration

  // PLAY THE SPEAKER
  outputValue     = map(sensorValue, cal_sensorMin, cal_sensorMax, 200, 1000); // map the value to produce high or low pitches
  spk_bipInterval = map(outputValue, 200, 1000, 800, 200);                     // interval between pitches

  if(millis() - spk_lastBip > spk_bipInterval) {
    tone(analogOutPin, outputValue, 10);
    spk_lastBip = millis();
  }
  
  delay(2); // wait 2 milliseconds before the next loop for the analog-to-digital converter to settle
}


/*
 * name: peakToPeak([integer] pin used for the mic, [integer] duration of the measure);
 * description: measure the microphone's value based on a serie of measures
 * return: [integer] value read from microphone pin
 */
int peakToPeak(int pin, int duration) {
  unsigned long record_start = millis();  // Start of sample window
  int record_max = 0;
  int record_min = 1024;
  
  // collect data for record_duration miliseconds
  while (millis() - record_start < duration) {
    // reads the sensor and stores into a variable
    int sensorReading = analogRead(pin);
    
    if (sensorReading > record_max) {
      record_max = sensorReading;  // save just the max levels
    } else if (sensorReading < record_min) {
      record_min = sensorReading;  // save just the min levels
    }
  }

  return record_max - record_min;  // peak-peak amplitude
}
</code></pre>